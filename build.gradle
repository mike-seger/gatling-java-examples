plugins {
    id 'java'
    id 'io.gatling.gradle' version '3.8.4'
}

ext {
    reflectionsVersion= '0.10.2'
}

group 'com.net128'
version '1.0-SNAPSHOT'

dependencies {
    gatling "org.reflections:reflections:${reflectionsVersion}"
}

configurations {
    fatJarDependencies.extendsFrom gatling
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}

task fatJar(type: Jar, dependsOn: ['gatlingClasses', 'processResources']) {
    group =  "build"
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Main-Class': 'com.net128.testing.load.Application'
    }

    exclude 'META-INF/MANIFEST.MF'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveClassifier = "all"

    from files(sourceSets.main.output.classesDirs)
    from files(sourceSets.gatling.output)
    from {
        configurations['fatJarDependencies']
            .collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

tasks.named("jar") { finalizedBy("fatJar") }

gatling {
  // WARNING: options below only work when logback config file isn't provided
  logLevel = 'WARN' // logback root level
  logHttp = 'NONE' // set to 'ALL' for all HTTP traffic in TRACE, 'FAILURES' for failed HTTP traffic in DEBUG
}

repositories {
  mavenCentral()
}
